plugins {
    id 'java-library'
    id 'maven'
    id 'signing'
}

group 'com.ecfeed'
version '2.0.0-SNAPSHOT'

java {
    modularity.inferModulePath = true
}

//test {
//    useJUnitPlatform()
//}

javadoc {
    include 'com/ecfeed/annotation/*'
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: gradle.sonatypeUsername, password: gradle.sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: gradle.sonatypeUsername, password: gradle.sonatypePassword)
            }

            pom.project {
                name 'ecFeed'
                packaging 'jar'
                description '' +
                        'An open library used to connect to the ecFeed service. ' +
                        'It is integrated with Junit5 and generates a stream of test cases using a selected algorithm (e.g. Cartesian, N-Wise).'
                url 'https://ecfeed.com/'

                scm {
                    url 'scm:git@github.com:ecfeed/ecfeed.java.git'
                    connection 'scm:git@github.com:ecfeed/ecfeed.java.git'
                    developerConnection 'scm:git@github.com:ecfeed/ecfeed.java.git'
                }

                licenses {
                    license {
                        name 'Eclipse Public License - v 1.0'
                        url 'https://www.eclipse.org/legal/epl-v10.html'
                    }
                }

                developers {
                    developer {
                        id 'p.chamuczynski'
                        name 'Patryk Chamuczynski'
                        email 'p.chamuczynski@testify.no'
                    }
                    developer {
                        id 'marek'
                        name 'Marek Kuczabinski'
                        email 'm.kuczabinski@testify.no'
                    }
                    developer {
                        id 'k.skorupski'
                        name 'Krzysztof Skorupski'
                        email 'k.skorupski@testify.no'
                    }
                    developer {
                        id 'izulin'
                        name 'Przemyslaw Uznanski'
                        email 'p.uznanski@testify.no'
                    }
                }

            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.json:json:20200518'
    implementation 'org.apache.httpcomponents:httpclient:4.5.12'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0-M1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.0-M1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0-M1'
    testImplementation 'org.assertj:assertj-core:3.6.1'
}
